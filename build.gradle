plugins {
  id "com.github.hierynomus.license" version "0.14.0" apply false
  id "com.github.jk1.dependency-license-report" version "0.3.11" apply false
}

allprojects {
  apply plugin: 'idea'
  apply plugin: 'java'
  apply plugin: "com.github.jk1.dependency-license-report"
  apply plugin: 'com.github.hierynomus.license'

  sourceCompatibility = 1.8
  targetCompatibility = 1.8

  license {
    header rootProject.file('codequality/HEADER')
    exclude '**/*.template'
  }

  if (project.path != ':') {
    buildDir = new File(rootDir, 'build/' + project.path.replace(':', '-').substring(1))
  } else {
    buildDir = new File(rootDir, 'build/' + project.name)
  }

  group = 'com.linkedin.gradle.portal'
  version = '0.0.1'
  
  repositories {
    mavenCentral()
  }
}

subprojects {
  apply plugin: 'ivy-publish'

  publishing {
    publications {
      ivyJava(IvyPublication) {
        from components.java
      }
    }
    repositories {
      ivy {
        url "${System.getProperty("user.home")}/local-repo"
        layout('pattern') {
          ivy "[organisation]/[module]/[revision]/[module]-[revision].ivy"
          artifact "[organisation]/[module]/[revision]/[artifact]-[revision](-[classifier]).[ext]"
          m2compatible true
        }
      }
    }
  }
}